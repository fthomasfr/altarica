/*******************************************************************************
* Copyright (c) 2011-2012 Frédéric Thomas.
* All rights reserved. This program and the accompanying materials
* are made available under the terms of the Eclipse Public License v1.0
* which accompanies this distribution, and is available at
* http://www.eclipse.org/legal/epl-v10.html
*
* Contributors:
* fthomas - initial grammar
*******************************************************************************/

grammar fr.onair.dsl.safety.altarica.Altarica with org.eclipse.xtext.common.Terminals

generate altarica "http://www.onair.fr/dsl/safety/Altarica"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

System:
	ownedDeclarations+=AbstractDeclaration*;

AbstractDeclaration:
	Domain | ConstantDefinition | Node;

ConstantDefinition:
	'const' constant=Constant expression=AbstractDefinitionConstant ';';

Constant:
	name=ID;

AbstractDefinitionConstant:
	ExpressionConstant | DomainConstant;

ExpressionConstant:
	'=' ownedExpression=Expression;

DomainConstant:
	':' domain=AbstractDomain ('=' ownedExpression=Expression)?;

Domain:
	'domain' name=ID '=' domain=AbstractDomain ';';

AbstractDomain:
	PrimitiveType | Range | Enumeration;

Range:
	'[' lower=Atom ',' upper=Atom ']';

Enumeration:
	'{' ownedLiterals+=Literal (',' ownedLiterals+=Literal)* '}';

PrimitiveType:
	name=PrimitiveTypeKind;

enum PrimitiveTypeKind:
	INTEGER='integer' | BOOLEAN='bool';

Literal:
	name=ID;

Node:
	'node' name=ID 
		ownedSpecifications+=AbstractSpecification*
	'edon';

AbstractSpecification:
	FlowSpecification | StateSpecification | EventSpecification | NodeInstanceSpecification | AssertSpecification |
	VectorSpecification | TransitionSpecification | InitSpecification | ExternalSpecification;

VariableAttribute:
	name=ID;

InitSpecification:
	'init' ownedInitStatements+=InitStatement*;

InitStatement:
	affectation=Affectation ";";

ExternalSpecification:
	'extern' ownedDirectives+=ExternalDirective*;

ExternalDirective:
	directive = SL_COMMENT;

//terminal Directive returns ecore::EString : "/*" -> ";";

FlowSpecification:
	'flow' ownedDeclarations+=FlowDeclaration*;

FlowDeclaration:
	ownedFlows+=Flow (',' ownedFlows+=Flow)* ':' domain=AbstractTypeRef (':' kind=FlowKind)? (':' attribute=VariableAttribute)? ";";

enum FlowKind:
	IN='in' | OUT='out' | INOUT='inout';

enum VisibilityKind:
	PUBLIC='public' | PRIVATE='private' | PARENT='parent';

Flow:
	name=ID;

EventSpecification:
	'event' ownedEventDeclarations+=EventDeclaration*;

EventDeclaration:
	ownedEvents+=Event (',' ownedEvents+=Event)* (':' attribute=VariableAttribute)? ';';

Event:
	name=ID ('[' ownedPriority=Priority ']')?;

Priority:
	('!' | 'priority') ownedExpression=AbstractExpression;

StateSpecification:
	'state' ownedStateDeclarations+=StateDeclaration*;

StateDeclaration:
	ownedStates+=State (',' ownedStates+=State)* ':' domain=AbstractTypeRef (':' attribute=VariableAttribute)? ";";

State:
	name=ID;

AbstractTypeRef:
	DomainRef | AbstractDomain;

DomainRef:
	reference=[Domain];

NodeInstanceSpecification:
	'sub' ownedNodeInstanceDeclarations+=NodeInstanceDeclaration*;

NodeInstanceDeclaration:
	ownedNodeInstances+=NodeInstance (',' ownedNodeInstances+=NodeInstance)* ':' nodeType=[Node] ";";

NodeInstance:
	name=ID;

AssertSpecification:
	'assert' ownedAsserts+=Assert*;

Assert:
	ownedExpressions+=AbstractBooleanExpression ";";

VectorSpecification:
	'sync' ownedVectors+=Vector*;

Vector:
	'<' ownedParameters+=VectorParameter (',' ownedParameters+=VectorParameter)* '>' ('[' cardinality=Cardinality ']')? ";";

VectorParameter:
	eventParameter= EventRef (isRequired?='?')?;

Cardinality:
	('>' expression=AbstractExpression) | ('>=' expression=AbstractExpression) | ('=' expression=AbstractExpression);

TransitionSpecification:
	'trans' ownedTransitionTransitions+=Transition*;

Transition:
	ownedExpresion=AbstractExpression '|-' event=[Event] '->' (ownedAffectations+=Affectation (','
	ownedAffectations+=Affectation)*)* ";";

Affectation:
	state=[State] ':=' ownedExpression=AbstractExpression;

AbstractExpression:
	IfThenElse| Switch | Expression;

AbstractBooleanExpression:
	IfThenElse| Switch | BooleanExpression;
	
Switch:
	'case {' 
		ownedCases+=CaseExpression*
	'else'
		default=Expression
	'}';

CaseExpression:
	condition=BooleanExpression ':' body=Expression ',';

IfThenElse:
	'if' condition=BooleanExpression 'then' then=(Switch | Expression) 'else' else=(Switch | Expression);

Expression :
	Minus ({Addition.leftOperand=current} '+' rightOperand=Minus)*;

Minus returns Expression:
	Multiplication ({Minus.leftOperand=current} '-' rightOperand=Multiplication)*;

Multiplication returns Expression:
	Division ({Multiplication.leftOperand=current} '*' rightOperand=Division)*;

Division returns Expression:
	 BooleanExpression ({Division.leftOperand=current} '/' rightOperand=BooleanExpression)*;



BooleanExpression returns Expression:
	And;
	
And returns Expression:
	Or ({And.leftOperand=current} ('and'|'&') rightOperand=Or)*;

Or returns Expression:
	Equal ({Or.leftOperand=current} ('or'|'|') rightOperand=Equal)*;

Equal returns Expression:
	NotEqual ({Equal.leftOperand=current} '=' rightOperand=NotEqual)*;

NotEqual returns Expression:
	StrictLower ({NotEqual.leftOperand=current} '!=' rightOperand=StrictLower)*;

StrictLower returns Expression:
	Lower ({StrictLower.leftOperand=current} '<' rightOperand=Lower)*;

Lower returns Expression:
	StrictUpper ({Lower.leftOperand=current} '<=' rightOperand=StrictUpper)*;

StrictUpper returns Expression:
	Upper ({StrictUpper.leftOperand=current} '>' rightOperand=Upper)*;

Upper returns Expression:
	Imply ({Upper.leftOperand=current} '>=' rightOperand=Imply)*;

Imply returns Expression:
	Not ({Imply.leftOperand=current} '=>' rightOperand=Not)*;

Not returns Expression:
	(('~'|'not')operand = Atom) | Atom;

Atom returns Expression:
	BooleanLiteral | StringLiteral | IntegerLiteral | VariableRef | '(' Expression ')';

EventRef:
	variable=[NavigableVariable] ({NestedQualifiedEventRef.target=current} '.' nestedVariable=[NavigableVariable])*; //('.' ownedFeature=QualifiedEventRef)?;

VariableRef:
	variable=[NavigableVariable] ({NestedQualifiedVariableRef.target=current} '.' nestedVariable=[NavigableVariable])*;

NavigableVariable:
	NonNavigableVariable | NodeInstance | Event;
	
NonNavigableVariable:
	State | Literal | Constant | Flow;

BooleanLiteral returns EBoolean:
	value=('true' | 'false');

StringLiteral returns EString:
	value=STRING;

IntegerLiteral returns EInteger:
	value=INT;